<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.10">
  <POU Name="FB_StrategyController_Conventional" Id="{5bcaa7e7-a00f-4330-83cf-2ca503e06025}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_StrategyController_Conventional
VAR_INPUT
	ambientState : ST_AmbientState;
	
	//actual temperatures in thermal networks
	cnState : ST_ThermalNetworkState;
	hnltState : ST_ThermalNetworkState;
	hnhtState : ST_ThermalNetworkState;
END_VAR
VAR_OUTPUT	
	cnControl : ST_CoolingNetworkControl;
	hnltControl : ST_HeatingNetworkLowTemperatureControl;	
	hnhtControl : ST_HeatingNetworkHighTemperatureControl;
	hnltCnLinkageControl : ST_HnltCnLinkageControl;
	hnhtHnltLinkageControl : ST_HnhtHnltLinkageControl;
	hnhtCnLinkageControl : ST_HnhtCnLinkageControl;
END_VAR
VAR
	{attribute 'OPC.UA.DA' := '1'}
	localSetParameters : ST_StrategySetParameters;	
	{attribute 'OPC.UA.DA' := '1'}
	localState : ST_StrategyState;	

	//default parameters
	localDefaultParameters : ST_StrategyDefaultParameters;

	//internal variables
	fTargetTemperature_HNHT_Heating : REAL;
	fTargetTemperature_HNHT_Cooling : REAL;
	fTargetTemperature_HNLT_Cooling : REAL;
	fTargetTemperature_HNLT_Heating : REAL;
	fTargetTemperature_CN : REAL;
	aTemperatureLimits_HNHT : ARRAY[1..2] OF REAL;
	aTemperatureLimits_HNLT : ARRAY[1..2] OF REAL;
	aTemperatureLimits_CN : ARRAY[1..2] OF REAL;
	stHNHTCheckDefaultParameters : ST_RangeCheckDefaultParameters := (tTaskCycleTime := GlobalVariables.tCtrlCycleTime_MAIN, tCtrlCycleTime := GlobalVariables.tCtrlCycleTime_MAIN, fRestoreDifference := 5);
	HNHT_Check : FB_RangeCheck := (ST_DefaultParameters := stHNHTCheckDefaultParameters);
	stHNLTCheckDefaultParameters : ST_RangeCheckDefaultParameters := (tTaskCycleTime := GlobalVariables.tCtrlCycleTime_MAIN, tCtrlCycleTime := GlobalVariables.tCtrlCycleTime_MAIN, fRestoreDifference := 5);
	HNLT_Check : FB_RangeCheck := (ST_DefaultParameters := stHNLTCheckDefaultParameters);
	stCNCheckDefaultParameters : ST_RangeCheckDefaultParameters := (tTaskCycleTime := GlobalVariables.tCtrlCycleTime_MAIN, tCtrlCycleTime := GlobalVariables.tCtrlCycleTime_MAIN, fRestoreDifference := 2);
	CN_Check : FB_RangeCheck := (ST_DefaultParameters := stCNCheckDefaultParameters);
	
	//controller and variables for high temperature heating network

	Controller_CondensingBoiler : FB_CTRL_2POINT;
	Controller_CondensingBoiler_Parameters : ST_CTRL_2POINT_PARAMS;
	Controller_StaticHeating : FB_CTRL_2POINT;
	Controller_StaticHeating_Parameters : ST_CTRL_2POINT_PARAMS;
	
	//controllers and variables for low temperature heating network
	Controller_OuterCapillaryTubeMats : FB_CTRL_2POINT;
	Controller_OuterCapillaryTubeMats_Parameters : ST_CTRL_2POINT_PARAMS;
	Controller_OuterCapillaryTubeMats_Permission : FB_CTRL_2POINT;
	Controller_OuterCapillaryTubeMats_Permission_Parameters : ST_CTRL_2POINT_PARAMS;
	fOffset_TargetTemperature_OuterCapillaryTubeMats_Cooling : REAL := 1;	
	fOffset_OuterCapillaryTubeMats_Permission : REAL := 10;
	
	//controllers and variables for cooling network
	fOffset_TargetTemperature_eChiller : REAL := 0.5;	
	Controller_eChiller : FB_CTRL_2POINT;
	Controller_eChiller_Parameters : ST_CTRL_2POINT_PARAMS;	

	//controllers and variables for hnht and hnlt linkage
	Controller_HeatExchanger1_HNHT : FB_CTRL_2POINT;
	Controller_HeatExchanger1_HNHT_Parameters : ST_CTRL_2POINT_PARAMS;	
	Controller_HeatExchanger1_HNLT : FB_CTRL_2POINT;
	Controller_HeatExchanger1_HNLT_Parameters : ST_CTRL_2POINT_PARAMS;
	fOffset_TargetTemperature_HeatExchanger1 : REAL := 2;

	//clock
	stDefaultParameters_Clock : ST_ClockDefaultParameters := (tTaskCycleTime := GlobalVariables.tCtrlCycleTime_MAIN, tCtrlCycleTime := T#1M);
	Clock : FB_Clock := (ST_DefaultParameters := stDefaultParameters_Clock);
END_VAR

]]></Declaration>
    <Implementation>
      <ST><![CDATA[Clock();
AmbientControl();
SetTemperatures();
ControlCN();
ControlHNLT();
ControlHNHT();
ControlHnltCnLinkage();
ControlHnhtHnltLinkage();
SetState();]]></ST>
    </Implementation>
    <Method Name="AmbientControl" Id="{a6b017a6-8916-4340-b9ee-30288e1c6bf3}">
      <Declaration><![CDATA[METHOD AmbientControl : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//check for heating or cooling mode
IF Clock.stClockState.sintMonth>4 AND Clock.stClockState.sintMonth<10 THEN
	//heating mode
	hnltCnLinkageControl.bHeatingMode := FALSE;
	hnhtCnLinkageControl.bHeatingMode := FALSE;
ELSE
	//cooling mode	
	hnltCnLinkageControl.bHeatingMode := TRUE;
	hnhtCnLinkageControl.bHeatingMode := TRUE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="ControlCN" Id="{37ed791d-3674-4d0f-be12-d0e72a17b92e}">
      <Declaration><![CDATA[METHOD ControlCN : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//set hysteresis parameters for two-point controllers in cooling network
Controller_eChiller_Parameters.fHysteresisRange := 7;
Controller_eChiller_Parameters.tCtrlCycleTime := GlobalVariables.tCtrlCycleTime_MAIN;
Controller_eChiller_Parameters.tTaskCycleTime := GlobalVariables.tTaskCycleTime_MAIN;

//control eChiller
Controller_eChiller(fSetpointValue := cnState.fMidTemperature,
	fActualValue := fTargetTemperature_CN-fOffset_TargetTemperature_eChiller,
	eMode := eCTRL_MODE_ACTIVE,
	stParams := Controller_eChiller_Parameters);

cnControl.bSetStatusOn_eChiller := Controller_eChiller.bOut;

//set HFVA off
cnControl.bSetStatusOn_HVFASystem := FALSE;
cnControl.bLoading_HVFASystem := FALSE;

]]></ST>
      </Implementation>
    </Method>
    <Method Name="ControlHNHT" Id="{759911bb-f653-41da-a82f-62eed466366c}">
      <Declaration><![CDATA[METHOD ControlHNHT : BOOL
VAR_INPUT
END_VAR
VAR
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Controller_CondensingBoiler_Parameters.fHysteresisRange := 12;
Controller_CondensingBoiler_Parameters.tCtrlCycleTime := GlobalVariables.tCtrlCycleTime_MAIN;
Controller_CondensingBoiler_Parameters.tTaskCycleTime := GlobalVariables.tTaskCycleTime_MAIN;

Controller_StaticHeating_Parameters.fHysteresisRange := 4;
Controller_StaticHeating_Parameters.tCtrlCycleTime := GlobalVariables.tCtrlCycleTime_MAIN;
Controller_StaticHeating_Parameters.tTaskCycleTime := GlobalVariables.tTaskCycleTime_MAIN;


IF (Clock.stClockState.sintWeekday > 0 AND Clock.stClockState.sintWeekday < 6) AND (Clock.stClockState.fHour > 5 AND Clock.stClockState.fHour < 23) THEN
	hnhtControl.bSetStatusOn_CHP1 := TRUE;
	hnhtControl.bSetStatusOn_CHP2 := TRUE;
ELSE
	hnhtControl.bSetStatusOn_CHP1 := FALSE;
	hnhtControl.bSetStatusOn_CHP2 := FALSE;
END_IF

//condensing boiler is switched if network temperature falls below critical temperature limit
Controller_CondensingBoiler(fSetpointValue := fTargetTemperature_HNHT_Heating,
	fActualValue := hnhtState.fUpperTemperature,
	eMode := eCTRL_MODE_ACTIVE,
	stParams := Controller_CondensingBoiler_Parameters);
hnhtControl.bSetStatusOn_CondensingBoiler := Controller_CondensingBoiler.bOut;


hnhtControl.bSetStatusOn_VSIStorage := FALSE;
hnhtControl.bLoading_VSISystem := FALSE;


Controller_StaticHeating(fSetpointValue := 15,
	fActualValue := ambientState.fOutsideTemperature,
	eMode := eCTRL_MODE_ACTIVE,
	stParams := Controller_StaticHeating_Parameters);
hnhtControl.bSetStatusOn_StaticHeating := Controller_StaticHeating.bOut;




]]></ST>
      </Implementation>
    </Method>
    <Method Name="ControlHnhtHnltLinkage" Id="{0aa7fac4-ad3a-4e78-b43c-83ad05ef7890}">
      <Declaration><![CDATA[METHOD ControlHnhtHnltLinkage : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Controller_HeatExchanger1_HNHT_Parameters.fHysteresisRange := 8;
Controller_HeatExchanger1_HNHT_Parameters.tCtrlCycleTime := GlobalVariables.tCtrlCycleTime_MAIN;
Controller_HeatExchanger1_HNHT_Parameters.tTaskCycleTime := GlobalVariables.tTaskCycleTime_MAIN;

Controller_HeatExchanger1_HNLT_Parameters.fHysteresisRange := 8;
Controller_HeatExchanger1_HNLT_Parameters.tCtrlCycleTime := GlobalVariables.tCtrlCycleTime_MAIN;
Controller_HeatExchanger1_HNLT_Parameters.tTaskCycleTime := GlobalVariables.tTaskCycleTime_MAIN;


//control heat exchanger 1 from HNHT
Controller_HeatExchanger1_HNHT(fSetpointValue := hnhtState.fUpperTemperature,
	fActualValue := fTargetTemperature_HNHT_Cooling,
	eMode := eCTRL_MODE_ACTIVE,
	stParams := Controller_HeatExchanger1_HNHT_Parameters);

//control heat exchanger 1 from HNLT
Controller_HeatExchanger1_HNLT(fSetpointValue := fTargetTemperature_HNLT_Heating-fOffset_TargetTemperature_HeatExchanger1,
	fActualValue := hnltState.fMidTemperature,
	eMode := eCTRL_MODE_ACTIVE,
	stParams := Controller_HeatExchanger1_HNLT_Parameters);
	
hnhtHnltLinkageControl.bSetStatusOn_HeatExchanger1 := Controller_HeatExchanger1_HNHT.bOut OR Controller_HeatExchanger1_HNLT.bOut;

//turn heat pump off
hnhtHnltLinkageControl.bSetStatusOn_HeatPump := FALSE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="ControlHNLT" Id="{d74db99c-7af6-4e0c-8450-647efa501511}">
      <Declaration><![CDATA[METHOD ControlHNLT : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//set hysteresis parameters for two-point controllers in cooling network
Controller_OuterCapillaryTubeMats_Parameters.fHysteresisRange := 6;
Controller_OuterCapillaryTubeMats_Parameters.tCtrlCycleTime := GlobalVariables.tCtrlCycleTime_MAIN;
Controller_OuterCapillaryTubeMats_Parameters.tTaskCycleTime := GlobalVariables.tTaskCycleTime_MAIN;

Controller_OuterCapillaryTubeMats_Permission_Parameters.fHysteresisRange := 6;
Controller_OuterCapillaryTubeMats_Permission_Parameters.tCtrlCycleTime := GlobalVariables.tCtrlCycleTime_MAIN;
Controller_OuterCapillaryTubeMats_Permission_Parameters.tTaskCycleTime := GlobalVariables.tTaskCycleTime_MAIN;


//control strategy for cooling network - fSetpointValue and fActualValue are changed for cooling applications
Controller_OuterCapillaryTubeMats(fSetpointValue := hnltState.fMidTemperature,
	fActualValue := fTargetTemperature_HNLT_Cooling+fOffset_TargetTemperature_OuterCapillaryTubeMats_Cooling,
	eMode := eCTRL_MODE_ACTIVE,
	stParams := Controller_OuterCapillaryTubeMats_Parameters);

//permission controller to ensure that network ist not heated up if ambient temperature is too high
Controller_OuterCapillaryTubeMats_Permission(fSetpointValue := fTargetTemperature_HNLT_Cooling-fOffset_OuterCapillaryTubeMats_Permission,
	fActualValue := ambientState.fOutsideTemperature,
	eMode := eCTRL_MODE_ACTIVE,
	stParams := Controller_OuterCapillaryTubeMats_Permission_Parameters);

hnltControl.bSetStatusOn_OuterCapillaryTubeMats := Controller_OuterCapillaryTubeMats.bOut AND Controller_OuterCapillaryTubeMats_Permission.bOut;

// turn HFVA off
hnltControl.bSetStatusOn_HVFASystem := FALSE;
hnltControl.bLoading_HVFASystem := FALSE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ControlHnltCnLinkage" Id="{c755dd8d-c1a9-4445-9586-38e81a29090b}">
      <Declaration><![CDATA[METHOD ControlHnltCnLinkage : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// set heat pump off
hnltCnLinkageControl.bSetStatusOn_HeatPump := FALSE;


hnltCnLinkageControl.fTargetTemperature_ProductionHall := localDefaultParameters.fTargetTemperature_ProductionHall;

hnltCnLinkageControl.bSetStatusOn_InnerCapillaryTubeMats := TRUE;
hnltCnLinkageControl.bSetStatusOn_UnderfloorHeating := TRUE;

]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetState" Id="{5f19b6e4-e3ed-4747-a661-143dab192d67}">
      <Declaration><![CDATA[METHOD SetState : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[localState.fTargetTemperature_HNHT_Heating := fTargetTemperature_HNHT_Heating;
localState.fTargetTemperature_HNHT_Cooling := fTargetTemperature_HNHT_Cooling;
localState.fTargetTemperature_HNLT_Heating := fTargetTemperature_HNLT_Heating;
localState.fTargetTemperature_HNLT_Cooling := fTargetTemperature_HNLT_Cooling;
localState.fTargetTemperature_CN := fTargetTemperature_CN;
localState.fTargetTemperature_ProductionHall := localDefaultParameters.fTargetTemperature_ProductionHall;

localState.aTemperatureLimits_HNHT := aTemperatureLimits_HNHT;
localState.aTemperatureLimits_HNLT := aTemperatureLimits_HNLT;
localState.aTemperatureLimits_CN := aTemperatureLimits_CN;	

localState.fFeedTemperature_HNHT := fTargetTemperature_HNHT_Heating+localDefaultParameters.fOffset_FeedTemperature_HNHT;
localState.fFeedTemperature_HNLT_Heating := fTargetTemperature_HNLT_Heating+localDefaultParameters.fOffset_FeedTemperature_HNLT_Heating;
localState.fFeedTemperature_HNLT_Cooling := fTargetTemperature_HNLT_Cooling-localDefaultParameters.fOffset_FeedTemperature_HNLT_Cooling;
localState.fFeedTemperature_CN := fTargetTemperature_CN-localDefaultParameters.fOffset_FeedTemperature_CN;]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetTemperatures" Id="{327b041d-a699-4ebf-91c9-25c7a386bfc5}">
      <Declaration><![CDATA[METHOD SetTemperatures : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//set internal variables depending on prodution mode
IF localSetParameters.bProductionModeActivated THEN
	fTargetTemperature_CN := localDefaultParameters.fTargetTemperature_CN_ProductionMode_Summer;	
	fTargetTemperature_HNLT_Heating := localDefaultParameters.fTargetTemperature_HNLT_Heating_ProductionMode;
	fTargetTemperature_HNLT_Cooling := localDefaultParameters.fTargetTemperature_HNLT_Cooling_ProductionMode;
	fTargetTemperature_HNHT_Heating := localDefaultParameters.fTargetTemperature_HNHT_Heating_ProductionMode;
	fTargetTemperature_HNHT_Cooling := localDefaultParameters.fTargetTemperature_HNHT_Cooling_ProductionMode;
	aTemperatureLimits_CN := localDefaultParameters.aTemperatureLimits_CN_ProductionMode;
	aTemperatureLimits_HNLT := localDefaultParameters.aTemperatureLimits_HNLT_ProductionMode;
	aTemperatureLimits_HNHT := localDefaultParameters.aTemperatureLimits_HNHT_ProductionMode;

ELSE
	fTargetTemperature_CN := localDefaultParameters.fTargetTemperature_CN_Summer;
	fTargetTemperature_HNLT_Heating := localDefaultParameters.fTargetTemperature_HNLT_Heating;
	fTargetTemperature_HNLT_Cooling := localDefaultParameters.fTargetTemperature_HNLT_Cooling;
	fTargetTemperature_HNHT_Heating := localDefaultParameters.fTargetTemperature_HNHT_Heating;
	fTargetTemperature_HNHT_Cooling := localDefaultParameters.fTargetTemperature_HNHT_Cooling;
	aTemperatureLimits_CN := localDefaultParameters.aTemperatureLimits_CN;
	aTemperatureLimits_HNLT := localDefaultParameters.aTemperatureLimits_HNLT;
	aTemperatureLimits_HNHT := localDefaultParameters.aTemperatureLimits_HNHT;
END_IF

//set feed temperatures
hnhtControl.fFeedTemperature := fTargetTemperature_HNHT_Heating+localDefaultParameters.fOffset_FeedTemperature_HNHT;
hnltControl.fFeedTemperature_Heating := fTargetTemperature_HNLT_Heating+localDefaultParameters.fOffset_FeedTemperature_HNLT_Heating;
hnltControl.fFeedTemperature_Cooling := fTargetTemperature_HNLT_Cooling-localDefaultParameters.fOffset_FeedTemperature_HNLT_Cooling;
cnControl.fFeedTemperature := fTargetTemperature_CN-localDefaultParameters.fOffset_FeedTemperature_CN;
hnhtHnltLinkageControl.fFeedTemperature_HNHT := hnhtControl.fFeedTemperature;
hnhtHnltLinkageControl.fFeedTemperature_HNLT_Heating := hnltControl.fFeedTemperature_Heating;
hnhtHnltLinkageControl.fFeedTemperature_HNLT_Cooling := hnltControl.fFeedTemperature_Cooling;
hnltCnLinkageControl.fFeedTemperature_CN := cnControl.fFeedTemperature;
hnltCnLinkageControl.fFeedTemperature_HNLT_Heating := hnltControl.fFeedTemperature_Heating;
hnltCnLinkageControl.fFeedTemperature_HNLT_Cooling := hnltControl.fFeedTemperature_Cooling;

//set algorithm permission depending on production mode to control structs
CN_Check(fInput := cnState.fMidTemperature,
	fLowerLimit := aTemperatureLimits_CN[1],
	fUpperLimit := aTemperatureLimits_CN[2]);
cnControl.bAlgorithmPermission := CN_Check.bPermission;
	
HNLT_Check(fInput := hnltState.fMidTemperature,
	fLowerLimit := aTemperatureLimits_HNLT[1],
	fUpperLimit := aTemperatureLimits_HNLT[2]);
hnltControl.bAlgorithmPermission := HNLT_Check.bPermission;
	
HNHT_Check(fInput := hnhtState.fMidTemperature,
	fLowerLimit := aTemperatureLimits_HNHT[1],
	fUpperLimit := aTemperatureLimits_HNHT[2]);	
hnhtControl.bAlgorithmPermission := HNHT_Check.bPermission;
	
hnltCnLinkageControl.bAlgorithmPermission_CN := cnControl.bAlgorithmPermission;
hnltCnLinkageControl.bAlgorithmPermission_HNLT := hnltControl.bAlgorithmPermission;
hnhtHnltLinkageControl.bAlgorithmPermission_HNHT := hnhtControl.bAlgorithmPermission;
hnhtHnltLinkageControl.bAlgorithmPermission_HNLT := hnltControl.bAlgorithmPermission;
hnhtCnLinkageControl.bAlgorithmPermission_CN := cnControl.bAlgorithmPermission;
hnhtCnLinkageControl.bAlgorithmPermission_HNHT := hnhtControl.bAlgorithmPermission;]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_StrategyController_Conventional">
      <LineId Id="387" Count="0" />
      <LineId Id="389" Count="0" />
      <LineId Id="112" Count="0" />
      <LineId Id="168" Count="1" />
      <LineId Id="221" Count="0" />
      <LineId Id="170" Count="0" />
      <LineId Id="313" Count="0" />
      <LineId Id="57" Count="0" />
    </LineIds>
    <LineIds Name="FB_StrategyController_Conventional.AmbientControl">
      <LineId Id="6" Count="3" />
      <LineId Id="16" Count="0" />
      <LineId Id="10" Count="2" />
      <LineId Id="17" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_StrategyController_Conventional.ControlCN">
      <LineId Id="51" Count="2" />
      <LineId Id="40" Count="0" />
      <LineId Id="87" Count="0" />
      <LineId Id="44" Count="0" />
      <LineId Id="66" Count="3" />
      <LineId Id="89" Count="0" />
      <LineId Id="161" Count="1" />
      <LineId Id="156" Count="1" />
      <LineId Id="155" Count="0" />
      <LineId Id="140" Count="0" />
      <LineId Id="133" Count="0" />
    </LineIds>
    <LineIds Name="FB_StrategyController_Conventional.ControlHNHT">
      <LineId Id="52" Count="1" />
      <LineId Id="51" Count="0" />
      <LineId Id="78" Count="2" />
      <LineId Id="77" Count="0" />
      <LineId Id="104" Count="0" />
      <LineId Id="293" Count="1" />
      <LineId Id="281" Count="0" />
      <LineId Id="295" Count="0" />
      <LineId Id="297" Count="1" />
      <LineId Id="300" Count="0" />
      <LineId Id="299" Count="0" />
      <LineId Id="284" Count="0" />
      <LineId Id="56" Count="4" />
      <LineId Id="54" Count="0" />
      <LineId Id="285" Count="0" />
      <LineId Id="290" Count="2" />
      <LineId Id="127" Count="0" />
      <LineId Id="82" Count="4" />
      <LineId Id="272" Count="0" />
      <LineId Id="270" Count="0" />
      <LineId Id="87" Count="0" />
      <LineId Id="133" Count="0" />
      <LineId Id="94" Count="0" />
      <LineId Id="61" Count="0" />
    </LineIds>
    <LineIds Name="FB_StrategyController_Conventional.ControlHnhtHnltLinkage">
      <LineId Id="13" Count="1" />
      <LineId Id="10" Count="0" />
      <LineId Id="60" Count="2" />
      <LineId Id="59" Count="0" />
      <LineId Id="45" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="6" Count="0" />
      <LineId Id="67" Count="2" />
      <LineId Id="66" Count="0" />
      <LineId Id="70" Count="0" />
      <LineId Id="65" Count="0" />
      <LineId Id="7" Count="2" />
      <LineId Id="5" Count="0" />
      <LineId Id="63" Count="0" />
      <LineId Id="22" Count="0" />
      <LineId Id="73" Count="0" />
      <LineId Id="72" Count="0" />
      <LineId Id="71" Count="0" />
    </LineIds>
    <LineIds Name="FB_StrategyController_Conventional.ControlHNLT">
      <LineId Id="6" Count="3" />
      <LineId Id="41" Count="2" />
      <LineId Id="39" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="10" Count="5" />
      <LineId Id="50" Count="0" />
      <LineId Id="44" Count="0" />
      <LineId Id="47" Count="2" />
      <LineId Id="45" Count="0" />
      <LineId Id="147" Count="0" />
      <LineId Id="146" Count="0" />
      <LineId Id="148" Count="0" />
      <LineId Id="142" Count="0" />
      <LineId Id="141" Count="0" />
      <LineId Id="133" Count="0" />
      <LineId Id="19" Count="0" />
    </LineIds>
    <LineIds Name="FB_StrategyController_Conventional.ControlHnltCnLinkage">
      <LineId Id="115" Count="1" />
      <LineId Id="114" Count="0" />
      <LineId Id="118" Count="0" />
      <LineId Id="117" Count="0" />
      <LineId Id="58" Count="0" />
      <LineId Id="110" Count="0" />
      <LineId Id="79" Count="0" />
      <LineId Id="78" Count="0" />
      <LineId Id="106" Count="0" />
      <LineId Id="76" Count="0" />
    </LineIds>
    <LineIds Name="FB_StrategyController_Conventional.SetState">
      <LineId Id="22" Count="0" />
      <LineId Id="6" Count="12" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_StrategyController_Conventional.SetTemperatures">
      <LineId Id="18" Count="0" />
      <LineId Id="6" Count="0" />
      <LineId Id="8" Count="0" />
      <LineId Id="126" Count="0" />
      <LineId Id="55" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="128" Count="0" />
      <LineId Id="14" Count="0" />
      <LineId Id="16" Count="1" />
      <LineId Id="15" Count="0" />
      <LineId Id="10" Count="0" />
      <LineId Id="127" Count="0" />
      <LineId Id="12" Count="0" />
      <LineId Id="56" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="129" Count="0" />
      <LineId Id="20" Count="1" />
      <LineId Id="19" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="60" Count="2" />
      <LineId Id="64" Count="0" />
      <LineId Id="67" Count="1" />
      <LineId Id="63" Count="0" />
      <LineId Id="69" Count="2" />
      <LineId Id="73" Count="0" />
      <LineId Id="72" Count="0" />
      <LineId Id="50" Count="0" />
      <LineId Id="38" Count="0" />
      <LineId Id="82" Count="2" />
      <LineId Id="92" Count="1" />
      <LineId Id="88" Count="1" />
      <LineId Id="85" Count="0" />
      <LineId Id="94" Count="1" />
      <LineId Id="90" Count="1" />
      <LineId Id="39" Count="0" />
      <LineId Id="96" Count="1" />
      <LineId Id="51" Count="0" />
      <LineId Id="34" Count="0" />
      <LineId Id="108" Count="0" />
      <LineId Id="77" Count="0" />
      <LineId Id="109" Count="1" />
    </LineIds>
  </POU>
</TcPlcObject>