<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.10">
  <POU Name="FB_UnderfloorHeatingSystem" Id="{4efa52ab-487d-4cce-a8ee-6c4090219ec6}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_UnderfloorHeatingSystem EXTENDS FB_SystemContinous
VAR_INPUT
	bHeatingModeAutomatic : BOOL; //control mode: 0 = heating mode, 1 = cooling mode
	stAmbientState : ST_AmbientState;
END_VAR
VAR_OUTPUT
END_VAR
VAR
	{attribute 'OPC.UA.DA' := '1'}
	localSetParameters : ST_UnderfloorHeatingSetParameters;

	//default parameters
	localDefaultParameters : ST_UnderfloorHeatingDefaultParameters;	
	
	//pumps
	stPU425DefaultParameters : ST_PumpGrundfosMagna3DefaultParameters;
	PU425 : FB_PumpGrundfosMagna3 := (ST_DefaultParameters := stPU425DefaultParameters);	
	
	//valves
	stSV423DefaultParameters : ST_DiscreteValveDefaultParameters := (nFeedback := 3);
	stSV424DefaultParameters : ST_DiscreteValveDefaultParameters := (nFeedback := 3);
	stRV425DefaultParameters : ST_MixingValveDefaultParameters := (bFeedbackSignal := FALSE, tTaskCycleTime := GlobalVariables.tTaskCycleTime_MAIN, tCtrlCycleTime := GlobalVariables.tCtrlCycleTime_MAIN);	
	
	SV423 : FB_DiscreteValve := (ST_DefaultParameters := stSV423DefaultParameters); 
	SV424 : FB_DiscreteValve := (ST_DefaultParameters := stSV424DefaultParameters);
	RV425 : FB_MixingValve := (ST_DefaultParameters := stRV425DefaultParameters);
	WMZ425 : FB_MollineEHeatCounter;
	
	//internal variables
	bHeatingMode 			: BOOL;
	bSetStatusOn_PU425 		: BOOL;
	bSetStatusOn_SV423		: BOOL;
	bSetStatusOn_SV424		: BOOL;
	bSetStatusOn_RV425 		: BOOL;
	fSetPoint_Internal 		: REAL;
	nControlMode_RV425 		: E_MixingValveControlMode; 	
	
	//characteristic curves and controllers
	CharacteristicCurve 			: FB_CTRL_FLOW_TEMP_SETPOINT_GEN;
	CharacteristicCurve_Parameters	: ST_CTRL_FLOW_TEMP_SETPOINT_GEN_PARAMS;
	
	//triggers
	SwitchToHeating : R_TRIG;
	SwitchToCooling : F_TRIG;
	bFinishedSwitchMode : BOOL := TRUE;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[//check if heating mode has switched
SelectLocalControlMode();
LocalControl();
fSetPointAutomatic := fSetPoint_Internal;
SwitchToHeating(CLK := bHeatingMode);
SwitchToCooling(CLK := bHeatingMode);

//MAIN must be called before SwitchMode to prevent pump signal chattering (signal is written after SwitchMode function), SwitchMode may only be called if system is on
MAIN();

//disrupt calling SystemFlowControl via MAIN() when mode is switched and call SwitchMode()
IF ((SwitchToHeating.Q OR SwitchToCooling.Q) OR NOT bFinishedSwitchMode) AND bSetStatusOn THEN
	SwitchMode(bSwitchToHeating := bHeatingMode,
		bFinishedSwitchMode => bFinishedSwitchMode);
END_IF;

//pumps
PU425.GetState_Profibus();
PU425(bSetStatusOnAutomatic := bSetStatusOn_PU425,
	fSetPointAutomatic := 100,
	bAlgorithmPermission := bAlgorithmPermission,
	nControlModeAutomatic := 0);
PU425.SetState_Profibus();

//valves in thermal networks
RV425.GetState_AI();
RV425(bSetStatusOnAutomatic := bSetStatusOn_RV425,
	fSetPointAutomatic := fSetPoint_Internal,
	bAlgorithmPermission := bAlgorithmPermission,
	fTemperatureExternal := PU425.fTemperatureInternal,
	nControlModeAutomatic := nControlMode_RV425);
RV425.SetState_AO();

SV423.GetState_BI();
SV423(bSetStatusOnAutomatic := bSetStatusOn_SV423,
	bAlgorithmPermission := bAlgorithmPermission);
SV423.SetState_BO();

SV424.GetState_BI();
SV424(bSetStatusOnAutomatic := bSetStatusOn_SV423,
	bAlgorithmPermission := bAlgorithmPermission);
SV424.SetState_BO();

WMZ425(aHeatCounterTelegramm := aTelegramm_WMZ425);]]></ST>
    </Implementation>
    <Method Name="CheckError" Id="{b9b5fe8e-e7ff-4396-9357-ddff10f773aa}">
      <Declaration><![CDATA[METHOD PROTECTED  CheckError : BOOL
VAR_INPUT
END_VAR
VAR_OUTPUT
	bError : BOOL;
END_VAR
VAR
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//check pumps for errors
bError :=  PU425.bError;]]></ST>
      </Implementation>
    </Method>
    <Method Name="LocalControl" Id="{33d239a8-7df3-480e-902e-e6461385ded3}">
      <Declaration><![CDATA[METHOD PRIVATE LocalControl : BOOL
VAR_INPUT
END_VAR
VAR
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CharacteristicCurve_Parameters.fAmbientTempMax := localDefaultParameters.aLimitsAmbientTemperature[2];
CharacteristicCurve_Parameters.fAmbientTempMin := localDefaultParameters.aLimitsAmbientTemperature[1];
CharacteristicCurve_Parameters.fForeRunTempMax := localDefaultParameters.aLimitsFeedTemperature[1];
CharacteristicCurve_Parameters.fForeRunTempMin := localDefaultParameters.aLimitsFeedTemperature[2];
CharacteristicCurve_Parameters.tCtrlCycleTime := GlobalVariables.tCtrlCycleTime_MAIN;
CharacteristicCurve_Parameters.tTaskCycleTime := GlobalVariables.tTaskCycleTime_MAIN;

CharacteristicCurve(fAmbientTemperature := stAmbientState.fOutsideTemperature,
	fOffsetTemperature := 0,
	bLimitFlowTemp := TRUE,
	stParams := CharacteristicCurve_Parameters);
fSetPoint_Internal := LREAL_TO_REAL(CharacteristicCurve.fOut);
	
IF bHeatingMode THEN
	nControlMode_RV425 := 1;
ELSE
	nControlMode_RV425 := 2;	
END_IF

]]></ST>
      </Implementation>
    </Method>
    <Method Name="Operational" Id="{b3659eec-c263-4bae-bfc6-63fd4bfd8556}">
      <Declaration><![CDATA[METHOD PROTECTED  Operational : BOOL
VAR_INPUT
END_VAR
VAR_OUTPUT
	bSwitchToWorking : BOOL := FALSE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[bSetStatusOn_RV425 := TRUE;
bSetStatusOn_PU425 := TRUE;

IF PU425.bStatusOn THEN
	bSwitchToWorking := TRUE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="RampDown" Id="{9173a3c4-04c3-4530-96c4-77af03b435f0}">
      <Declaration><![CDATA[METHOD PROTECTED  RampDown : BOOL
VAR_INPUT
END_VAR
VAR_OUTPUT
	bRampDownFinished : BOOL := FALSE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[bSetStatusOn_PU425 := FALSE;
bSetStatusOn_RV425 := FALSE;
	
IF PU425.bStatusOn THEN
	bRampDownFinished := TRUE;		
END_IF	
]]></ST>
      </Implementation>
    </Method>
    <Method Name="RampUp" Id="{ed258e40-1b81-4df7-9768-c25938f5bb5f}">
      <Declaration><![CDATA[METHOD PROTECTED  RampUp : BOOL
VAR_INPUT
END_VAR
VAR_OUTPUT
	bRampUpFinished : BOOL := FALSE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF bHeatingMode THEN
	//bSetStatusOn_SV423 := FALSE;
	//bSetStatusOn_SV424 := FALSE;
	IF SV423.localState.nPosition=3 AND SV424.localState.nPosition=3 THEN
		bRampUpFinished := TRUE;		
	END_IF	
ELSE
	//bSetStatusOn_SV423 := TRUE;
	//bSetStatusOn_SV424 := TRUE;
	IF SV423.localState.nPosition=2 AND SV424.localState.nPosition=2 THEN
		bRampUpFinished := TRUE;		
	END_IF	
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="SelectLocalControlMode" Id="{7be6bcd7-4518-40f0-81a5-7bb610014654}">
      <Declaration><![CDATA[METHOD PRIVATE SelectLocalControlMode : BOOL
VAR_INPUT
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF control.bManualModeActivated THEN
	bHeatingMode := localSetParameters.bHeatingModeManual;
ELSIF control.bAlgorithmModeActivated AND bAlgorithmPermission THEN
	bHeatingMode := localSetParameters.bHeatingModeAlgorithm;
ELSE
	bHeatingMode := bHeatingModeAutomatic;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetSetPointLimits" Id="{7a91eedd-ad13-4b09-b075-bc9157871f45}">
      <Declaration><![CDATA[METHOD PROTECTED   SetSetPointLimits : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[aSetPointLimits[1] := localDefaultParameters.aSetPointLimitsTemperature[1];
aSetPointLimits[2] := localDefaultParameters.aSetPointLimitsTemperature[2];]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetState" Id="{5e916aa9-de43-48ca-bf50-12137a9ce652}">
      <Declaration><![CDATA[METHOD PROTECTED  SetState : BOOL
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[fOperatingPoint := PU425.fTemperatureInternal;

controlState.bStatusOn := bSetStatusOn;
setPointState.aSetPointLimits := aSetPointLimits;
setPointState.fSetPoint := fSetPoint;
setPointState.fOperatingPoint := fOperatingPoint;
setPointState.sSetPointUnit := 'degreeC';
setPointState.sOperatingPointUnit := 'degreeC';]]></ST>
      </Implementation>
    </Method>
    <Method Name="SwitchMode" Id="{db0b0ee2-1b40-45b2-95bd-b1cb07d07506}">
      <Declaration><![CDATA[METHOD PRIVATE SwitchMode : BOOL
VAR_INPUT
	bSwitchToHeating : BOOL; //if false switch to cooling
END_VAR
VAR_OUTPUT
	bFinishedSwitchMode : BOOL := FALSE;
END_VAR
VAR_INST
	bRampDownFinished : BOOL := FALSE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//check system state
IF bRampDownFinished THEN
	IF bSwitchToHeating THEN
		//false - valve connects heating network
		bSetStatusOn_SV423 := FALSE;
		bSetStatusOn_SV424 := FALSE;	

		IF SV423.localState.nPosition=3 AND SV424.localState.nPosition=3 THEN
			//reset instant variable and switch on pump
			bRampDownFinished := FALSE;			
			bFinishedSwitchMode := TRUE;
			bSetStatusOn_PU425 := TRUE;
		END_IF
		
	ELSE
		//true - connects cooling network
		bSetStatusOn_SV423 := TRUE;
		bSetStatusOn_SV424 := TRUE;	

		IF SV423.localState.nPosition=2 AND SV424.localState.nPosition=2 THEN
			//reset instant variable and switch on pump
			bRampDownFinished := FALSE;			
			bFinishedSwitchMode := TRUE;
			bSetStatusOn_PU425 := TRUE;
		END_IF
	END_IF
ELSE
	bSetStatusOn_PU425 := FALSE;
	IF NOT PU425.bStatusOn THEN
		bRampDownFinished := TRUE;
	END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Working" Id="{7c525aa6-95b6-42f5-b5b1-3574bb70abb3}">
      <Declaration><![CDATA[METHOD PROTECTED  Working : BOOL
VAR_INPUT
END_VAR
VAR_OUTPUT
	bSwitchToOperational : BOOL := FALSE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[//empty method]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_UnderfloorHeatingSystem">
      <LineId Id="903" Count="42" />
      <LineId Id="812" Count="0" />
    </LineIds>
    <LineIds Name="FB_UnderfloorHeatingSystem.CheckError">
      <LineId Id="9" Count="0" />
      <LineId Id="8" Count="0" />
    </LineIds>
    <LineIds Name="FB_UnderfloorHeatingSystem.LocalControl">
      <LineId Id="114" Count="0" />
      <LineId Id="120" Count="4" />
      <LineId Id="126" Count="0" />
      <LineId Id="125" Count="0" />
      <LineId Id="139" Count="3" />
      <LineId Id="127" Count="0" />
      <LineId Id="31" Count="0" />
      <LineId Id="148" Count="0" />
      <LineId Id="150" Count="1" />
      <LineId Id="149" Count="0" />
      <LineId Id="44" Count="0" />
      <LineId Id="23" Count="0" />
    </LineIds>
    <LineIds Name="FB_UnderfloorHeatingSystem.Operational">
      <LineId Id="14" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="21" Count="1" />
      <LineId Id="20" Count="0" />
      <LineId Id="23" Count="0" />
    </LineIds>
    <LineIds Name="FB_UnderfloorHeatingSystem.RampDown">
      <LineId Id="13" Count="0" />
      <LineId Id="30" Count="0" />
      <LineId Id="19" Count="3" />
      <LineId Id="14" Count="0" />
    </LineIds>
    <LineIds Name="FB_UnderfloorHeatingSystem.RampUp">
      <LineId Id="15" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="45" Count="0" />
      <LineId Id="37" Count="1" />
      <LineId Id="36" Count="0" />
      <LineId Id="19" Count="0" />
      <LineId Id="56" Count="4" />
      <LineId Id="17" Count="0" />
    </LineIds>
    <LineIds Name="FB_UnderfloorHeatingSystem.SelectLocalControlMode">
      <LineId Id="6" Count="5" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_UnderfloorHeatingSystem.SetSetPointLimits">
      <LineId Id="9" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_UnderfloorHeatingSystem.SetState">
      <LineId Id="8" Count="6" />
      <LineId Id="7" Count="0" />
    </LineIds>
    <LineIds Name="FB_UnderfloorHeatingSystem.SwitchMode">
      <LineId Id="5" Count="0" />
      <LineId Id="50" Count="0" />
      <LineId Id="16" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="125" Count="0" />
      <LineId Id="108" Count="0" />
      <LineId Id="107" Count="0" />
      <LineId Id="60" Count="0" />
      <LineId Id="71" Count="0" />
      <LineId Id="59" Count="0" />
      <LineId Id="87" Count="0" />
      <LineId Id="46" Count="0" />
      <LineId Id="37" Count="0" />
      <LineId Id="29" Count="0" />
      <LineId Id="38" Count="0" />
      <LineId Id="126" Count="7" />
      <LineId Id="40" Count="0" />
      <LineId Id="27" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="53" Count="0" />
      <LineId Id="91" Count="2" />
      <LineId Id="52" Count="0" />
    </LineIds>
    <LineIds Name="FB_UnderfloorHeatingSystem.Working">
      <LineId Id="7" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>